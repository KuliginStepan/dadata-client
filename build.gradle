plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
    id 'signing'
}

apply from: 'gradle/publish.gradle'

group = 'com.github.kuliginstepan'
version = '2.5.3'
sourceCompatibility = '1.8'

def nexusUsername = System.getenv("NEXUS_USERNAME") ?: (project.findProperty("NEXUS_USERNAME") ?: "")
def nexusPassword = System.getenv("NEXUS_PASSWORD") ?: (project.findProperty("NEXUS_PASSWORD") ?: "")

nexusPublishing {
    repositories {
        sonatype {
            username = nexusUsername
            password = nexusPassword
        }
    }
}

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
}

configurations {
    internal {
        visible = false
        canBeConsumed = false
        canBeResolved = false
    }
    annotationProcessor {
        extendsFrom(internal)
    }
    testAnnotationProcessor {
        extendsFrom(internal)
    }
    compileClasspath {
        extendsFrom(internal)
    }
    runtimeClasspath {
        extendsFrom(internal)
    }
    testCompileClasspath {
        extendsFrom(internal)
    }
    testRuntimeClasspath {
        extendsFrom(internal)
    }
}

dependencies {
    internal platform('org.springframework.boot:spring-boot-starter-parent:2.2.6.RELEASE')
    internal platform('org.springframework.boot:spring-boot-starter-parent:2.2.6.RELEASE')
    implementation 'org.springframework.boot:spring-boot'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework:spring-webflux'
    implementation 'io.projectreactor.netty:reactor-netty'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.slf4j:slf4j-api'
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

test.finalizedBy jacocoTestReport
